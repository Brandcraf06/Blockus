plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${mod_version}+${minecraft_version}"
group = project.maven_group

repositories {
	maven { url 'https://server.bbkr.space/artifactory/libs-release' }
	maven { url 'https://maven.terraformersmc.com/' }
	maven { url "https://maven.shedaniel.me/" }
	maven { url 'https://www.cursemaven.com' }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"

	// Terraform API
  modImplementation "com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_wood_api_version}"
	include "com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_wood_api_version}"
  modCompileOnly "com.terraformersmc.terraform-api:terraform-shapes-api-v1:${project.terraform_shapes_api_version}"

	// Columns
  modCompileOnly "curse.maven:columns-385230:3697328"

	// Promenade
  modCompileOnly "curse.maven:promenade-399648:3553043"
  modCompileOnly "curse.maven:dawnapi-399309:3543407"
}

loom {
	accessWidenerPath = file("src/main/resources/blockus.aw")

	runs {
		datagen {
			server()

			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			runDir "build/datagen"
		}
	}
}
assemble.dependsOn(runDatagen)

sourceSets {
	main {
		resources {
			srcDirs += [
				"src/main/generated"
			]
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

  
